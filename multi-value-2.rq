#+ summary: Multi-value placeholder hack...
#+ method: GET
#+ description: Tested with Virtuoso and RDF4J SPARQL endpoints.

select ?n ?value ?uri {
  # Copy the block below into your SPARQL query to have a multi-value placeholder for grlc.
  # Optionally, rename the variables and/or change the separator.

  #=========================== MULTI-VALUE PLACEHOLDER ============================#
  { select ?value ?n {                                                                # <- VAR value
    { select * { optional { ?a ?b ?c } } limit 1 }                                 #
    bind(" " as ?separator)                                                        # <- CONST separator
    bind(?_placeholder as ?vs)                                                     # <- VAR _placeholder
    optional { <x:> <x:> ?_placeholder }                                           # <- VAR _placeholder
    values ?x{0 1 2 3 4 5 6 7 8 9} values ?y{0 1 2 3 4 5 6 7 8 9} bind((10*?x)+?y as ?n)  
    bind(concat("^([^ ]*",?separator,"){",str(?n),"}") as ?pattern)                #
    filter(if(?n=0, true, regex(?vs, ?pattern)))                                   #
    bind(replace(if(?n=0, ?vs, replace(?vs, ?pattern, "")), " .*", "") as ?value)  # <- VAR value
  } }                                                                              #
  #================================================================================#

  # The value is a literal. You can make a URI out of it like this:
  bind(uri(?value) as ?uri)
}
