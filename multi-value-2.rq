#+ summary: Multi-value placeholder hack...
#+ method: GET
#+ description: Tested with Virtuoso and RDF4J SPARQL endpoints.

select ?value ?uri {
  # Copy the block below into your SPARQL query to have a multi-value placeholder for grlc.
  # Optionally, rename the variables

  #=========================== MULTI-VALUE PLACEHOLDER ============================#
  { select ?value {                                                                # <- VARIABLE value
    { select * { optional { ?a ?b ?c } } limit 1 }                                 #
    bind(" " as ?separator)                                                        # <- SET separator (default is " ")
    bind(?_multi_placeholder as ?vs) optional { <x:> <x:> ?_multi_placeholder }    # <- VARIABLE _multi_placeholder (2x)
    values ?n { 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24   #
      25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 49   #
      50 51 52 53 54 55 56 57 58 59 60 61 62 63 64 65 66 67 68 69 70 71 72 73 74   #
      75 76 77 78 79 80 81 82 83 84 85 86 87 88 89 90 91 92 93 94 95 96 97 98 99}  #
    bind(concat("^([^ ]*",?separator,"){",str(?n),"}") as ?pattern)                #
    filter(if(?n=0, true, regex(?vs, ?pattern)))                                   #
    bind(replace(if(?n=0, ?vs, replace(?vs, ?pattern, "")), " .*", "") as ?value)  # <- VARIABLE value
  } }                                                                              #
  #================================================================================#

  # The value is a literal. You can make a URI out of it like this:
  bind(uri(?value) as ?uri)
}
