#+ summary: Multi-value placeholder hack...
#+ method: GET
#+ description: Tested with Virtuoso and RDF4J SPARQL endpoints.

select ?value ?uri {
  # Copy the block below into your SPARQL query to have a multi-value placeholder for grlc.
  # Optionally, rename the variables and/or change the separator.

  #===================== MULTI-VALUE PLACEHOLDER ======================#------ config: ------
  { select ?value {                                                    # <- VAR value
    bind(?_placeholder as ?vs)                                         # <- VAR _placeholder
    optional { <x:> <x:> ?_placeholder }  # (for grlc to pick up var)  # <- VAR _placeholder
    {select*{optional{?a ?b ?c}}limit 1}  # (for some Virtuoso inst.)  #
    values ?x {0 1 2 3 4 5 6 7 8 9} values ?y {0 1 2 3 4 5 6 7 8 9}    #
    bind((10*?x)+?y as ?n)  # (works up to a maximum of 100 values)    #
    bind(" " as ?sep)  # (separator as single regex-compatible char)   # <- CONST sep
    bind(concat("^([^",?sep,"]*",?sep,"){",str(?n),"}") as ?p)         #
    filter(if(?n=0,true,regex(?vs,?p)))                                #
    bind(replace(if(?n=0,?vs,replace(?vs,?p,""))," .*","") as ?value)  # <- VAR value
  } }                                                                  #
  #====================================================================#---------------------

  # The value is a literal. You can make a URI out of it like this:
  bind(uri(?value) as ?uri)
}
